# Maintainer: Martchus <martchus@gmx.net>
# Contributor (maintains regular package): Antonio Rojas <arojas@archlinux.org>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

_pkgname=kirigami2
_android_arch=aarch64

pkgname=android-$_android_arch-$_pkgname
pkgver=5.240.0.20231004
pkgrel=1
pkgdesc="A QtQuick based components set (Android, $_android_arch)"
arch=('any')
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=("android-$_android_arch-qt6"-{declarative,shadertools,svg,5compat})
makedepends=('cmake' 'android-ndk' 'android-sdk' 'android-cmake' 'extra-cmake-modules-git' 'ninja' 'git' 'qt6-base' 'qt6-declarative' 'qt6-tools' 'qt6-shadertools')
#source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$_pkgname-$pkgver.tar.xz"{,.sig})
#sha256sums=('9706915f219ef9394b030dbb24974d4fddef091008954cf75cf97cc97586e35f'
#            'SKIP')
_commit=41bdddb37ea96ea19109c2aa9e7158cb57986386
source=(git+https://invent.kde.org/frameworks/kirigami.git#commit=$_commit)
sha256sums=(SKIP)
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>
options=(!buildflags staticlibs !strip !emptydirs)

#pkgver() {
#  cd kirigami
#  echo "$pkgver.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
#}

build() {
  source android-env ${_android_arch}
  android-${_android_arch}-cmake -G Ninja -B build-$_android_arch -S kirigami \
    -DCMAKE_FIND_ROOT_PATH="${ANDROID_PREFIX}" \
    -DECM_DIR:PATH=/usr/share/ECM/cmake \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  VERBOSE=1 cmake --build build-$_android_arch
}

package() {
  source android-env ${_android_arch}
  DESTDIR="$pkgdir" VERBOSE=1 cmake --install build-$_android_arch
}
