# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=xcb-proto
pkgname=static-compat-$_pkgname
pkgver=1.16.0
pkgrel=1
pkgdesc="XML-XCB protocol descriptions"
arch=(any)
url="https://xcb.freedesktop.org/"
license=('custom')
makedepends=('python' 'libxml2' 'static-compat-configure')
source=(https://xorg.freedesktop.org/archive/individual/proto/$_pkgname-$pkgver.tar.xz{,.sig}
        0001_xcb-proto_revert_datarootdir.patch::https://gitlab.freedesktop.org/xorg/proto/xcbproto/-/commit/d849b9534a52b0199f26c1dd30ac11aba12db9dc.patch)
sha512sums=('54906f379a7aa2cbbfbbe54764c02dbf522ca8f6c54e07d4faae471aac3aee25d0359be38c808c8da887de228aed745582a78b2f56714b023bca8f3513b24972'
            'SKIP'
            'SKIP')
validpgpkeys=('A66D805F7C9329B4C5D82767CCC4F07FAC641EFF') # "Daniel Stone <daniels@collabora.com>"
validpgpkeys+=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"
validpgpkeys+=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

prepare() {
  source static-compat-environment
  cd $_pkgname-$pkgver
  # revert pkgfile back to /usr/lib
  patch -Rp1 -i ../0001_xcb-proto_revert_datarootdir.patch
  autoreconf -vfi
}

build() {
  source static-compat-environment
  cd $_pkgname-$pkgver
  static-compat-configure
  make
}

check() {
  source static-compat-environment
  cd $_pkgname-$pkgver
  make check
}

package() {
  source static-compat-environment
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
