_pkgbase=boost
pkgname=boost-doc
pkgver=1.86.0
_srcname=boost_${pkgver//./_}
pkgrel=1
pkgdesc='Free peer-reviewed portable C++ source libraries (HTML documentation)'
url='https://www.boost.org/'
arch=('x86_64')
license=('custom')
makedepends=()
source=(https://boostorg.jfrog.io/artifactory/main/release/$pkgver/source/$_srcname.tar.bz2)
sha256sums=('1bed88e40401b2cb7a1f76d4bab499e352fa4d0c5f31c0dbae64e24d34d7513b')
options=(!strip)

package() {
   cd ${_srcname}
   local dest=$pkgdir/usr/share/$_pkgbase/doc

   install -dm755 "$dest"
   find \( \
      -iname '*.htm' -o -iname '*.html' -o -iname '*.css' -o \
      -iname '*.png' -o -iname '*.gif' -o -iname '*.jpg' -o -iname '*.js' \
   \) -exec cp --target-directory="$dest" --parents --verbose {} +
}

# vim: ts=2 sw=2 et:
