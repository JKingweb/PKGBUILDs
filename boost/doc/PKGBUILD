_pkgbase=boost
pkgname=boost-doc
pkgver=1.85.0
_srcname=boost_${pkgver//./_}
pkgrel=1
pkgdesc='Free peer-reviewed portable C++ source libraries (HTML documentation)'
url='https://www.boost.org/'
arch=('x86_64')
license=('custom')
makedepends=()
source=(https://boostorg.jfrog.io/artifactory/main/release/$pkgver/source/$_srcname.tar.bz2)
sha256sums=('7009fe1faa1697476bdc7027703a2badb84e849b7b0baad5086b087b971f8617')
options=(!strip)

package() {
   cd ${_srcname}
   local dest=$pkgdir/usr/share/$_pkgbase/doc

   install -dm755 "$dest"
   find \( \
      -iname '*.htm' -o -iname '*.html' -o -iname '*.css' -o \
      -iname '*.png' -o -iname '*.gif' -o -iname '*.jpg' -o -iname '*.js' \
   \) -exec cp --target-directory="$dest" --parents --verbose {} +
}

# vim: ts=2 sw=2 et:
