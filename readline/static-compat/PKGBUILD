# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgname=readline
pkgname=static-compat-$_pkgname
_basever=8.2
_patchlevel=010
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc='GNU readline library'
arch=('x86_64')
url='https://tiswww.case.edu/php/chet/readline/rltop.html'
license=('GPL-3.0-only')
backup=('etc/inputrc')
depends=(glibc-static-compat static-compat-ncurses)
makedepends=(static-compat-configure)
options=('!emptydirs' 'staticlibs')
source=(https://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        inputrc)
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [ $_patchlevel -gt 0 ]; then
    for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//.}-$(printf "%03d" $_p){,.sig})
    done
fi

prepare() {
  cd $_pkgname-$_basever
  for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    msg "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  done

  # remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
  source static-compat-environment
  cd $_pkgname-$_basever

  # build with -fPIC for x86_64 (FS#15634)
  [[ $CARCH == "x86_64" ]] && CFLAGS="$CFLAGS -fPIC"

  static-compat-configure --disable-shared
  make SHLIB_LIBS=-lncurses
}

package() {
  source static-compat-environment
  make -C $_pkgname-$_basever DESTDIR="$pkgdir" install
  rm -r "$pkgdir/$static_compat_prefix"/share/{doc,info,man}
  find "$pkgdir/$static_compat_prefix"/lib -iname '*.so*' -delete
}

md5sums=('4aa1b31be779e6b84f9a96cb66bc50f6'
         'SKIP'
         '58d54966c1191db45973cb3191ac621a'
         'b800ce35690436a995a4fadf939e53cc'
         'SKIP'
         '22dcbd9d174166bc0cfbd4a6957603a0'
         'SKIP'
         '94a336980c337d7bf54c0191988055f1'
         'SKIP'
         'dabdab80dc147cffc6d5ef275b6daf67'
         'SKIP'
         '9e847d8b5243c364138ad9475de8c9b9'
         'SKIP'
         '8812592f2df5ce441b61b92fb63dc91f'
         'SKIP'
         'e57991bb85e2f025119ba78dde994665'
         'SKIP'
         '2a61c084c3867d85b99811d14a181f1d'
         'SKIP'
         'f5980a8a847f8c31c8ba4781562408db'
         'SKIP'
         'e3a188e5425bd2e17b32bd8626bfed16'
         'SKIP')
