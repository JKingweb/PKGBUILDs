# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

_pkgname=xz
pkgname=static-compat-$_pkgname
pkgver=5.4.1
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('x86_64')
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('glibc-static-compat')
makedepends=('static-compat-configure')
options=(!emptydirs staticlibs)
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620') # Lasse Collin <lasse.collin@tukaani.org>
source=("https://tukaani.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('e4b0f81582efa155ccf27bb88275254a429d44968e488fc94b806f2a61cd3e22'
            'SKIP')
sha512sums=('5cff8383a68fb88ecbb3770ec48af0ad5582e08de9dccd339e0b685aaa53447e59d6425caa3f63b54a674e5d78c20520876db547d156e6658ad4841660cba85b'
             'SKIP')

build() {
	source static-compat-environment
	cd "${srcdir}/${_pkgname}-${pkgver}"

	static-compat-configure --disable-shared --enable-static --disable-rpath
	make
}

check() {
	source static-compat-environment
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make check
}

package() {
	source static-compat-environment
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}/"
	ln -sf /usr/static/share/doc/xz/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
	rm -r "$pkgdir/$static_compat_prefix"/bin
	rm -r "$pkgdir/$static_compat_prefix"/share/{man,doc,locale}
}
